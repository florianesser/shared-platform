/**
 * Add groovy-all as dependency and ensure that it is used instead of the
 * individual groovy modules.
 * 
 * @param groovyVersion the groovy artifact version
 * @param addDependency if the dependency to groovy-all should be added
 * @param customName a custom name for the created bundle
 */
def groovyAll(String groovyVersion = '2.1.5', boolean addDependency = true, String customName = null) {
	configurations {
		platform {
			// groovy
			resolutionStrategy.eachDependency {
				if (it.requested.group == 'org.codehaus.groovy') {
					// always use groovy-all instead of (groovy-*)
					it.useTarget "org.codehaus.groovy:groovy-all:${groovyVersion}"
				}
			}
		}
	}
	
	platform {
		if (customName) {
			// custom symbolic name
			bnd('org.codehaus.groovy:groovy-all') {
				symbolicName = customName
			}
		}
		
		if (addDependency) {
			feature id: 'platform.shared.groovy-all',
					name: 'Groovy complete',
					version: groovyVersion, {
				bundle "org.codehaus.groovy:groovy-all:${groovyVersion}"
			}
		}
		
		imports(group: 'org.codehaus.groovy') {
			versionStrategy = MINIMUM // assume that groovy is backwards compatible
		}
	}
}