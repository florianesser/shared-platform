interface Const {
	static final String DEF_VERSION = '1.6.3'
	static final String ORIENT_GROUP = 'com.orientechnologies'
}

/**
 * Include Orient Graph database. Calls orientCore(...).
 * 
 * Be careful when using OrientDB - it may depend on SNAPSHOT versions
 * of the graph DB implementation that in turn depends on a different version
 * of OrientDB (for instance for version 1.5.1). This can lead to
 * inconsistencies. If possible, depend on a OrientDB release that does
 * not depend on SNAPSHOTS (if you can find one).
 */
def orientGraphDB(String orientDBVersion = Const.DEF_VERSION) {
	orientCore(orientDBVersion)
	
	platform {
		feature id: 'platform.shared.orientdb.graphdb',
				name: 'OrientDB Graph DB',
				version: orientDBVersion, {
			bundle group: Const.ORIENT_GROUP, name: 'orientdb-graphdb'
			
			includes << 'platform.shared.orientdb.core'
		}
		
		bnd group: 'com.tinkerpop.gremlin', name: 'gremlin-groovy', {
			// adapt gremlin configuration
			optionalImport 'com.tinkerpop.blueprints.impls.sail'
		}
	}
}

/**
 * Orient DB core bundles and fixed versions for all OrientDB bundles.
 */
def orientCore(String orientDBVersion = Const.DEF_VERSION) {
	def orientGroup = Const.ORIENT_GROUP
	
	repositories {
		maven {
			url 'https://oss.sonatype.org/content/groups/public/'
		}
	}
	
	configurations {
		platform {
			resolutionStrategy.eachDependency {
				if (it.requested.group == orientGroup) {
					it.useTarget(
						group: it.requested.group,
						name: it.requested.name,
						version: orientDBVersion // fixed version for all orient dependencies
					)
				}
			}
		}
	}
	
	platform {
		bnd group: orientGroup, {
			// import and export orient packages with version
			instruction 'Import-Package', "com.orientechnologies.*;version=${orientDBVersion},*"
			instruction 'Export-Package', "com.orientechnologies.*;version=${orientDBVersion},*"
		}

	        bnd name: 'concurrent', {
        	        instruction '-removeheaders', 'Require-Capability'
	        }
		
		feature id: 'platform.shared.orientdb.core',
				name: 'OrientDB core',
				version: orientDBVersion, {
			bundle group: orientGroup, name: 'orientdb-core'
			
			/*
			 * Commons and nativeos must share a classloader.
			 */
			merge {
				bundle group: orientGroup, name: 'orient-commons'
				bundle group: orientGroup, name: 'orientdb-nativeos'
				bnd {
					version = orientDBVersion
					symbolicName = 'com.orientechnologies.common'
				}
			}
		}
	}
}